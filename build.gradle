/*
 * Nature's master build file
 * Based on Natura's master build file
 * Authors: Sunstrike, ProgWML6
 * Help from AbrarSyed
 */

buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'ForgeFS'
            url = 'http://files.minecraftforge.net/maven'
        }
	maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }

    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}


repositories {
    maven {
        name 'CB Maven FS'
        url "http://chickenbones.net/maven/"
    }
    maven {
         name "ProfMobius Maven FS"
         url "http://mobiusstrip.eu/maven"
    }
    maven {
        name 'DVS1 Maven FS'
        url 'http://dvs1.progwml6.com/files/maven'
    }
    maven {
        name 'ForgeFS'
        url 'http://files.minecraftforge.net/maven'
    }
}


apply plugin: 'forge'
group = 'mods.natura'

file "build.properties" withReader {
    // read config.  it shall from now on be referenced as simply config or as project.config
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

dependencies {
    compile "codechicken:CodeChickenLib:${config.minecraft.version}-${config.CCLIB.version}:dev"
    compile "codechicken:ForgeMultipart:${config.minecraft.version}-${config.FMP.version}:dev"
    compile "codechicken:CodeChickenCore:${config.minecraft.version}-${config.ccc.version}:dev"
    compile "codechicken:NotEnoughItems:${config.minecraft.version}-${config.NEI.version}:dev"
}

minecraft {
    version = config.minecraft.version + "-" + config.forge.version
    runDir = 'run'
}

version = "${minecraft.version}-${config.project.version}"

sourceSets {
    main {
        resources {
            srcDir 'resources'
        }
    }
}

processResources
{
    from(sourceSets.main.resources.srcDirs) {
	include 'mcmod.info'

	// replace version and mcversion
	expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
	exclude 'mcmod.info'
    }
}

// because the normal output has been made to be obfuscated
task devjar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task sourcesjar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

artifacts {
    archives devjar
    archives sourcesjar
}
